1  REM A2DVI CONFIGURATION UTILITY
2  REM  COPYRIGHT 2024 THORSTEN BREHM
10  HOME
15  PRINT "A2DVI CONFIGURATION UTILITY":PRINT ""
20  INPUT "ENTER A2DVI SLOT (1-7): ";S
30  IF (S < 1)  OR (S >7)  THEN  PRINT "INVALID SLOT SELECTION!";CHR$(7):GOTO 20
40  HOME : VTAB 3: HTAB 7
50  PRINT "LOOK ON YOUR A2DVI SCREEN!"
60  VTAB 11: HTAB 8
70  PRINT "NO A2DVI CARD IN SLOT ";S;"!"
80  VTAB 19: HTAB 10
90  PRINT "PRESS CONTROL-RESET!"
100  REM CALCULATE CARD ADDRESS
110 S = -16256 + S*16
120  REM UNLOCK REGISTER AREA
150  POKE S+15,11:POKE S+15,22
160  REM MENU REGISTER AT $C0x9
170  M = S+9
180  POKE M,0
190  VTAB 21
200  GET A$
210  REM SEND KEY
220  POKE M, ASC(A$)
230  IF  ASC(A$) < >27  THEN  GOTO 200
240  REM LOCK REGISTER AREA
250  POKE S+15,0
300  HOME
310  HTAB 17: PRINT "A2DVI": PRINT ""
320  PRINT "CHANGES ARE LOST WHEN POWER-CYCLING,"
330  PRINT "UNLESS SETTINGS WERE SAVED TO FLASH."
340  PRINT ""
350  PRINT "HAVE A WONDERFUL DAY!"
360  PRINT "ENJOY YOUR APPLE II! :)"
370  END
