1  REM A2DVI CONFIGURATION UTILITY
2  REM  COPYRIGHT 2024 THORSTEN BREHM
10  HOME:VTAB 11
20  HTAB 9: PRINT "A = CONFIGURATION MENU"
30  HTAB 9: PRINT "B = UPLOAD CUSTOM FONTS"
40  GET A$
50  IF (A$ = "A")  OR (A$ = "a")  THEN  GOTO 1000
60  IF (A$ = "B")  OR (A$ = "b")  THEN  GOTO 2000
70  PRINT  CHR$(7)
80  GOTO 40
500  REM ASK FOR A2DVI SLOT
510  PRINT "ENTER A2DVI SLOT (1-7): ";
515  GET N$:PRINT N$
520  IF (N$ < "1")  OR (N$ > "7")  THEN  PRINT "INVALID SLOT SELECTION!"; CHR$(7): GOTO 510
525  N = ASC(N$)-48
530  REM CALCULATE CARD ADDRESS
540 S =  -16256 +N *16
550  REM UNLOCK REGISTER AREA
560  POKE S +15,11: POKE S +15,22
570  RETURN
600  REM SHOW A2DVI SCREEN HINT
610  HOME : VTAB 3: HTAB 7
620  PRINT "LOOK AT YOUR A2DVI SCREEN!"
630  VTAB 11: HTAB 8
640  PRINT "NO A2DVI CARD IN SLOT ";N;"!"
650  VTAB 19: HTAB 10
660  PRINT "PRESS CONTROL-RESET!"
670  RETURN
700  REM BYE
710  REM LOCK REGISTER AREA
720  POKE S +15,0
730  PRINT "HAVE A WONDERFUL DAY!"
740  PRINT "ENJOY YOUR APPLE II! :)"
750  END
1000  HOME
1010  PRINT "A2DVI CONFIGURATION UTILITY": PRINT ""
1020  GOSUB 500
1030  GOSUB 600
1100  REM MENU REGISTER AT $C0x9
1110 M = S +9
1120  POKE M,0
1130  VTAB 21
1140  GET A$
1150  REM SEND KEY
1160  POKE M, ASC(A$)
1170  IF  ASC(A$) < >27  THEN  GOTO 1140
1200  HOME
1210  HTAB 17: PRINT "A2DVI": PRINT ""
1220  PRINT "CHANGES ARE LOST WHEN POWER-CYCLING,"
1230  PRINT "UNLESS SETTINGS WERE SAVED TO FLASH."
1240  PRINT ""
1250  GOTO 700
2000  HOME
2010  PRINT "A2DVI CUSTOM FONT UTILITY": PRINT ""
2020  GOSUB 500
2100  INPUT "ENTER CUSTOM FONT NR (1-16): ";F
2110  IF (F <1)  OR (F >16)  THEN  PRINT "INVALID CUSTOM FONT NUMBER!"; CHR$(7): GOTO 2100
2120 F = F -1
2130  INPUT "ENTER CUSTOM FONT FILE NAME: ";N$
2200  PRINT ""
2210  PRINT "LOADING CUSTOM FONT ";N$
2300  GOSUB 3000: REM SET MEMORY MARKERS
2310 C$ = "BLOAD " +N$ +",A$2000"
2320  PRINT  CHR$(4);C$
2330  PRINT "": GOSUB 4000: REM DETECT FILE TYPE/SIZE
2340  IF C = 0  THEN  PRINT "INVALID VIDEO ROM FILE!": END
2350  REM ADJUST TYPE FOR APPLE IIE FONTS (C>1)
2360  IF C >1  THEN F = F +128
2400  REM SELECT FONT NUMBER TO BE UPLOADED (0..15)
2410  POKE S +2,F
2420 P = S +3
2500  REM UPLOAD FONT
2510  PRINT "": PRINT "UPLOADING FONT. STANDBY..."
2520  FOR I = 8192 TO 10239: POKE P, PEEK(I): NEXT I
2530  REM SAVE TO FLASH
2540  PRINT "WRITING CUSTOM FONT TO FLASH..."
2550  GOSUB 600
2560  POKE S +2,255
2570  GET A$
2580  HOME : PRINT "DONE."
2590  GOTO 700
3000  REM WRITE FILE SIZE MARKERS TO MEMORY
3010 M = 8192: GOSUB 3100
3020 M = 8192 +2048: GOSUB 3100
3030 M = 8192 +4096: GOSUB 3100
3040  RETURN
3100  FOR I = 0 TO 31: POKE M +I +2015,I: NEXT I
3110  RETURN
4000  REM CHECK FILE SIZE MARKERS IN MEMORY
4010 M = 8192 +4096: GOSUB 4100
4020  IF C = 1  THEN C = 3: PRINT "DETECTED 8KB APPLE IIE DUAL LANGUAGE ROM": RETURN
4030 M = 8192 +2048: GOSUB 4100
4040  IF C = 1  THEN C = 2: PRINT "DETECTED 4KB APPLE IIE VIDEO ROM": RETURN
4050 M = 8192: GOSUB 4100
4060  IF C = 1  THEN C = 1: PRINT "DETECTED 2KB APPLE II VIDEO ROM": RETURN
4070 C = 0: RETURN
4100  FOR I = 0 TO 31
4110  IF  PEEK(M +I +2015) < >I  THEN C = 1: RETURN
4120  NEXT I
4130 C = 0: RETURN
